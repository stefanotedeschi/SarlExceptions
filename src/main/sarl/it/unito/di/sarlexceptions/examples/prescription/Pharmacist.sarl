package it.unito.di.sarlexceptions.examples.prescription

import io.sarl.api.core.Behaviors
import io.sarl.api.core.DefaultContextInteractions
import io.sarl.api.core.Initialize
import io.sarl.api.core.Logging
import it.unito.di.sarlexceptions.events.ExceptionSpaceCreated
import it.unito.di.sarlexceptions.spaces.ExceptionSpace
import it.unito.di.sarlexceptions.spaces.ExceptionSpaceSpecification
import java.util.UUID

agent Pharmacist {
	
	uses DefaultContextInteractions, Behaviors,Logging

	var exSpacePatient : ExceptionSpace<LostMedicines>
	var exSpaceDoctor : ExceptionSpace<MissingPrescription>
	var prescriptionReceivedFromDoctor = false
	var medicinesSent = false

	on Initialize {
		val type = new ExceptionSpaceSpecification.class as Class<ExceptionSpaceSpecification<MissingPrescription>>
		exSpaceDoctor = defaultContext.getOrCreateSpaceWithID(type, UUID::randomUUID)
		exSpaceDoctor.registerAsRaiser(asEventListener)
		info("[Pharmacist] Registered as raiser for MissingPrescription!")
		emit(new ExceptionSpaceCreated(MissingPrescription, exSpaceDoctor.spaceID.ID))
		info("[Pharmacist] Initialized!")
	}

	on ExceptionSpaceCreated [occurrence.ex == LostMedicines] {
		exSpacePatient = defaultContext.getSpace(occurrence.id)
		exSpacePatient.registerAsHandler(asEventListener)
		info("[Pharmacist] Registered as handler for LostMedicines!")
    }
    
    on Prescribe {
		info("[Pharmacist] New prescription received from doctor! Sending medicines to patient...")
		prescriptionReceivedFromDoctor = true
		emit(new FillPrescription)
		medicinesSent = true
    }
	
	on LostMedicines {
		info("[Pharmacist] Handling exception LostMedicines!")
		if(medicinesSent) {
			emit(new FillPrescriptionAgain)
		}
		if (!prescriptionReceivedFromDoctor) {
			info("[Pharmacist] Raising exception Missing Prescription!")
			exSpaceDoctor.raiseException(new MissingPrescription(null),ID)
		}
	}
	
	on PrescribeFwd {
		info("[Pharmacist] New prescription received from doctor! Sending medicines to patient...")
		prescriptionReceivedFromDoctor = true
		emit(new FillPrescription)
		medicinesSent = true
	}
	
}
